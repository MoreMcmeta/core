/*
 * MoreMcmeta is a Minecraft mod expanding texture configuration capabilities.
 * Copyright (C) 2023 soir20
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation version 3 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */
import org.gradle.internal.os.OperatingSystem

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false
}

architectury {
    minecraft = project.minecraftVersion
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    task signJar() {
        doLast {
            if (project.hasProperty("keyStore")) {
                ant.signjar(
                        jar: remapJar.getArchiveFile().get().getAsFile().path,
                        keystore: project.keyStore,
                        alias: project.keyStoreAlias,
                        storepass: project.keyStorePass,
                        keypass: project.keyStoreKeyPass
                )
            }
        }
    }

    build.dependsOn(signJar)

    def lwjglNatives
    switch (OperatingSystem.current()) {
        case OperatingSystem.LINUX:
            lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            lwjglNatives = "natives-macos"
            break
        case OperatingSystem.WINDOWS:
            lwjglNatives = "natives-windows"
            break
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraftVersion}"
        mappings loom.officialMojangMappings()

        // Include plugins by default
        def final INCLUDE_DEFAULT_PLUGINS = (System.getenv("INCLUDE_DEFAULT_PLUGINS") ?: "true").toBoolean()
        if (INCLUDE_DEFAULT_PLUGINS && project.name != "common") {
            modRuntimeOnly(include("io.github.moremcmeta:json-parser-plugin-${project.name}:${project.jsonReaderVersion}"))
            modRuntimeOnly(include("io.github.moremcmeta:properties-parser-plugin-${project.name}:${project.propertiesReaderVersion}"))
            modRuntimeOnly(include("io.github.moremcmeta:animation-plugin-${project.name}:${project.animationVersion}"))
            modRuntimeOnly(include("io.github.moremcmeta:texture-plugin-${project.name}:${project.textureVersion}"))
            modRuntimeOnly(include("io.github.moremcmeta:gui-plugin-${project.name}:${project.guiVersion}"))
        }

        testImplementation "junit:junit:${project.junitVersion}"
        testImplementation "org.lwjgl:lwjgl:${rootProject.lwjglVersion}"
        testRuntimeOnly "org.lwjgl:lwjgl:${rootProject.lwjglVersion}:${lwjglNatives}"
    }

    processResources {
        filesMatching("**/*.mixins.json") {
            expand "javaVersion": project.javaVersion
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    def final SNAPSHOT_SUFFIX = "-prerelease"

    project.ext.modVersion = System.getenv("MOD_VERSION") ?: "DEV"
    project.ext.isSnapshot = project.modVersion.endsWith(SNAPSHOT_SUFFIX)

    // Include Minecraft version for snapshots since Forge requires the version to start with a number
    version = "${project.minecraftVersion}-${project.modVersion}"
    group = project.mavenGroup
    archivesBaseName = project.modArchivesBaseName

    project.ext.mavenArtifactId = "${archivesBaseName}-${project.name}"
    project.ext.mavenVersion = isSnapshot
            ? "${version.substring(0, version.length() - SNAPSHOT_SUFFIX.length())}-SNAPSHOT"
            : version

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = project.javaVersion.toInteger()
    }

    java {
        withSourcesJar()
    }

    repositories {
        maven {
            url "https://maven.pkg.github.com/moremcmeta/*/"
            credentials {
                username System.getenv("MOREMCMETA_MAVEN_USER") ?: project.moremcmetaMavenUser
                password System.getenv("MOREMCMETA_MAVEN_PASS") ?: project.moremcmetaMavenPass
            }
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = project.mavenArtifactId
                version = project.mavenVersion
                from components.java
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/MoreMcmeta/core"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}