plugins {
    id "architectury-plugin" version "3.1-SNAPSHOT"
    id "dev.architectury.loom" version "0.7.2-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraftVersion
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    task signJar() {
        doLast {
            if (project.hasProperty("keyStore")) {
                ant.signjar(
                        jar: remapJar.getArchiveFile().get().getAsFile().path,
                        keystore: project.keyStore,
                        alias: project.keyStoreAlias,
                        storepass: project.keyStorePass,
                        keypass: project.keyStoreKeyPass
                )
            }
        }
    }

    build.dependsOn(signJar)

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraftVersion}"
        mappings loom.officialMojangMappings()

        testCompile "junit:junit:${rootProject.junitVersion}"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    version = "${rootProject.minecraftVersion}-${rootProject.version}"
    group = rootProject.group
    archivesBaseName = rootProject.archivesBaseName

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release.set(targetVersion)
        }
    }

    java {
        withSourcesJar()
    }
}